# module PrecisionEstimate

#p = 41
#r = 7 # p-adic precision
#n = 2


"""
    frobenius_precision()

Determines the necessary p-adic precision r for the Frobenius matrix
"""
function frobenius_precision(k, q)
    if k == 2 && q == 7
        r = 2
    elseif k == 21 && q == 7
        r = 10
    end
    return r
end

"""
    impreciselog(p,a)

log with any base, via the log rules,
though I think there should be floating point error.
"""
impreciselog(p, a) = log(a) / log(p)

"""
    ibm_derham_bound(p,m,n)

ibm stands for integral basis multiplier

Calculates the smallest integer t such that
p^t* ω is in the integral latticee of the cohomology,
for all ω in the griffiths-dwork basis of cohomology.
Here, `m` is the pole order,
and the bound we have depends only on p, m, and n.

This is called ϕ in Costa's thesis, and it is
called f in Abbott-Kedlaya-Roe.
This method implements theorem 3.4.6 only of
Abbott-Kedlaya-Roe.
"""
function ibm_derham_bound(p, m, n)

    sum = 0
    for i = 1:n
        sum += floor(Int, impreciselog(p, max(1, m-i)))
    end

    sum
    #floor(Int,(n)*impreciselog(p,m))
end

"""
    ibm_constant(p,m,ibm_bound)

Calcuates the `N` in theorem 3.4.9 of Abbott-Kedlaya-Roe
"""
function ibm_constant(p, m, ibm_bound)
    g(m, i) = valuation(binomial(-m, i), p)

    NN(l) = ibm_bound[(m+l)*p] - l - g(m, l)

    l = 1
    N = NN(1)
    while 0 < NN(l)
        N = max(N, NN(l))
        l += 1
    end
    #verbose && println("In AKR notation, f((m+l)p) - l - g(m,l) < 0 at l = $l")

    N
end

"""
This algoritm gives the same
output as algorithm 3.4.10 of AKR,
even though it technically isn't the same.

Unlike that algorithm, we don't actually append to an array,
we simply update elements.

"""
function ibm_crank_bound(p, n, M)
    nChunks = ceil(Int, M/p)
    nBounds = nChunks * p
    bounds = ibm_derham_bound.(p, 1:(nBounds*p^2), n)

    for m = 1:nChunks
        c = 1
        while true
            N = ibm_constant(p, m, bounds)
            if N ≤ n - 1 + bounds[m]
                if n - 1 + bounds[m] ≤ bounds[p*m]
                    bounds[p*m] = n - 1 + bounds[m]
                end
                break
            elseif bounds[p*m] ≤ N
                if bounds[p*m] < N
                    #println("original bound is better than N!")
                end
                break
            else
                # go another round!
                bounds[p*m] = N
                #println("go another round!")
            end
            #println("finished crank number $c")
        end

        # update the elements before
        for k = ((p*m)-p+1):(p*m)
            if bounds[m*p] < bounds[k]
                bounds[k] = bounds[m*p]
            end
        end

    end

    #all(bounds .<= ibm_derham_bound.(p,1:nBounds*p^2,n)) || error("assertion failure")

    bounds[1:nBounds]
end

"""
    calculate_relative_precision(HP, slope, hodge_numbers, weight, p)

Calculates the vector of relative precisions r_m

INPUTS:
* "polygon" -- A SlopesPolygon struct describing a polygon whose values describe the
divisibility of the roots. Note: it is traditional to use the hodge polygon here,
but if you know the Newton Polygon (e.g. you have a K3 surface and you already have the
Artin-Mazur height) then the Newton Polygon will do just as well.
* "weight" -- integer, the motivic weight of the cohomology group, equals to the dimension of the hypersurface
* "p" -- integer, prime number
"""
function calculate_relative_precision(polygon, weight, p)

    #   * "HP" -- list, the i-th item corresponds to the height of above i in the Hodge polygon
    #   * "slope" -- list, the i-th item corresponds to the slope of the i-th segment in the Hodge polygon
    #   * "hodge_numbers" -- list, the list of Hodge numbers
    HP = polygon.values
    slope = Int.(polygon.slopesbefore)
    hodge_numbers = polygon.slopelengths

    r_vector = [0 for i = 1:length(hodge_numbers)]
    Pdeg = length(HP) - 1  # degree of the L-polynomial P_n(X,T)
    max_digits = 0

    for i = 1:(div(Pdeg, 2)+1)
        r = ceil(Int, log(2*Pdeg/i)/log(p) + i*weight*0.5) - HP[i+1]

        if r >= max_digits
            max_digits = r
            for j = 0:slope[i+1]
                r_vector[j+1] = r
            end

            for j = (slope[i+1]+1):(length(hodge_numbers)-1)
                r = r-1
                r_vector[j+1] = r
            end
        end
    end

    return reverse(r_vector)
end

function calculate_series_precision(p, n, r_m)

    # TODO: prove that this is correct
    #
    # We may assume in (1.8) of Costa's thesis that the worst case is i=0

    bounds = ibm_crank_bound(p, n, p*n)

    #TODO: why does ibm_derham_bound sometimes give answers that are better than the crank?

    N = [(r_m[m] == 0 ? 0 : r_m[m] - m + 1 + bounds[p*m]) for m = 1:n]

    #println("Series precision: $N")
    N
end

function series_precision(p, n, d, r_m)
    N = series_precision_default(p, n, d)

    if N === nothing
        N = calculate_series_precision(p, n, r_m)
    end
    N
end

"""
    algorithm_precision()

Determines M such that the algorithm works in Z/p^M Z
"""
function algorithm_precision(p, n, d, r_m, N_m)
    s_m = [i+x-1 for (i, x) in enumerate(N_m)]
    s_m_valuation = [valuation(ZZ(factorial(big(p*s-1))), ZZ(p)) for s in s_m]

    maximum([r_m[m] + s_m_valuation[m] - m + 1 for m = 1:length(r_m)])
end
